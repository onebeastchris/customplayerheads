plugins {
	id 'fabric-loom' version '1.6+'
	id 'maven-publish'
	id 'io.freefair.lombok' version '8.6'
	id "com.modrinth.minotaur" version "2.+"
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	maven {
		url = uri("https://repo.opencollab.dev/main/")
	}
}

dependencies {
    // To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.
	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

	// Floodgate Dependency.
	compileOnly("org.geysermc.floodgate:api:2.2.3-SNAPSHOT")

	include(implementation("org.spongepowered:configurate-hocon:4.1.2"))
	include(implementation("org.spongepowered:configurate-core:4.1.2"))
	include(implementation("com.typesafe:config:1.4.3"))
	include(implementation("io.leangen.geantyref:geantyref:1.3.14"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

modrinth {
	token.set(System.getenv("MODRINTH_TOKEN"))
	projectId.set("customplayerheads")
	versionNumber.set(project.version as String)
	versionType.set("release")
	uploadFile.set(remapJar)
	gameVersions = ["1.20.5", "1.20.6", "1.21"]
	loaders = ["fabric"]

	syncBodyFrom.set(rootProject.file("README.md").text)
	changelog.set(rootProject.file("CHANGELOG.md").text)

	failSilently.set(true)
}